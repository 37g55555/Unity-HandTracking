Shader "ShadowPrototype/UI Chroma Key"
{
    Properties
    {
        [PerRendererData] _MainTex ("Texture", 2D) = "white" {}
        _Color ("Tint", Color) = (1,1,1,1)
        _KeyColor ("Key Color", Color) = (1,0,1,1)
        _KeyColor2 ("Key Color 2", Color) = (1,0,1,1)
        _Threshold ("Threshold", Range(0,1)) = 0.22
        _Threshold2 ("Threshold 2", Range(0,1)) = 0.22
        _Softness ("Softness", Range(0.001,1)) = 0.08
        _Softness2 ("Softness 2", Range(0.001,1)) = 0.08
        _SpillReduction ("Spill Reduction", Range(0,1)) = 0.45

        _StencilComp ("Stencil Comparison", Float) = 8
        _Stencil ("Stencil ID", Float) = 0
        _StencilOp ("Stencil Operation", Float) = 0
        _StencilWriteMask ("Stencil Write Mask", Float) = 255
        _StencilReadMask ("Stencil Read Mask", Float) = 255
        _ColorMask ("Color Mask", Float) = 15
        [Toggle(UNITY_UI_ALPHACLIP)] _UseUIAlphaClip ("Use Alpha Clip", Float) = 0
    }

    SubShader
    {
        Tags
        {
            "Queue" = "Transparent"
            "IgnoreProjector" = "True"
            "RenderType" = "Transparent"
            "PreviewType" = "Plane"
            "CanUseSpriteAtlas" = "True"
        }

        Stencil
        {
            Ref [_Stencil]
            Comp [_StencilComp]
            Pass [_StencilOp]
            ReadMask [_StencilReadMask]
            WriteMask [_StencilWriteMask]
        }

        Cull Off
        Lighting Off
        ZWrite Off
        ZTest [unity_GUIZTestMode]
        Blend SrcAlpha OneMinusSrcAlpha
        ColorMask [_ColorMask]

        Pass
        {
            Name "Default"

            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #pragma target 2.0
            #pragma multi_compile_local _ UNITY_UI_CLIP_RECT
            #pragma multi_compile_local _ UNITY_UI_ALPHACLIP

            #include "UnityCG.cginc"
            #include "UnityUI.cginc"

            struct appdata_t
            {
                float4 vertex : POSITION;
                fixed4 color : COLOR;
                float2 texcoord : TEXCOORD0;
            };

            struct v2f
            {
                float4 vertex : SV_POSITION;
                fixed4 color : COLOR;
                float2 texcoord : TEXCOORD0;
                float4 worldPosition : TEXCOORD1;
            };

            sampler2D _MainTex;
            fixed4 _Color;
            fixed4 _KeyColor;
            fixed4 _KeyColor2;
            float _Threshold;
            float _Threshold2;
            float _Softness;
            float _Softness2;
            float _SpillReduction;
            float4 _ClipRect;

            v2f vert(appdata_t v)
            {
                v2f OUT;
                OUT.worldPosition = v.vertex;
                OUT.vertex = UnityObjectToClipPos(v.vertex);
                OUT.texcoord = v.texcoord;
                OUT.color = v.color * _Color;
                return OUT;
            }

            fixed4 frag(v2f IN) : SV_Target
            {
                fixed4 color = tex2D(_MainTex, IN.texcoord) * IN.color;
                float keyDistance = distance(color.rgb, _KeyColor.rgb);
                float keyDistance2 = distance(color.rgb, _KeyColor2.rgb);
                float alpha = smoothstep(_Threshold, _Threshold + _Softness, keyDistance);
                float alpha2 = smoothstep(_Threshold2, _Threshold2 + _Softness2, keyDistance2);
                alpha = min(alpha, alpha2);

                float spill = saturate(1.0 - alpha) * _SpillReduction;
                fixed3 spillColor = keyDistance <= keyDistance2 ? _KeyColor.rgb : _KeyColor2.rgb;
                color.rgb = saturate(color.rgb - spillColor * spill);
                color.a *= alpha;

                #ifdef UNITY_UI_CLIP_RECT
                color.a *= UnityGet2DClipping(IN.worldPosition.xy, _ClipRect);
                #endif

                #ifdef UNITY_UI_ALPHACLIP
                clip(color.a - 0.001);
                #endif

                return color;
            }
            ENDCG
        }
    }
}
